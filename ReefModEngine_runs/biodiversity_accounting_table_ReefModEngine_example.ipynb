{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "9f02a54a",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Activate packages in the manifest of this directory\n",
    "using Pkg; Pkg.activate(@__DIR__)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e997426b",
   "metadata": {},
   "source": [
    "# Create a biodiversity accounting table from ReefModEngine.jl results\n",
    "This script gives an example of creating a biodiversity accounting summary data table as a csv from a set of ReefModEngine.jl results. The metrics which make up the Reef Biodiversity Condition Index are calculated for all reefs and scenarios and reformatted into a CSV format for analysis in excel."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "6f3ca37e",
   "metadata": {},
   "source": [
    "## Load packages\n",
    "\n",
    "First, we load all the packages required to create the accounting table, including packages for managing CSV and net cdf file formats, a statistics package for calculating means and stds and a package for dealing with spatial dataframes."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "8bdb8e3b",
   "metadata": {},
   "outputs": [],
   "source": [
    "using ReefModEngine\n",
    "using CSV, DataFrames\n",
    "using GeoDataFrames\n",
    "using NCDatasets\n",
    "using Statistics"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "0475775b",
   "metadata": {},
   "source": [
    "## Load data summary functions\n",
    "\n",
    "The following functions are used to calculate the means and stds of the metrics for different counterfactual and intervention scenarios, and the difference in counterfactual and intervention, and store them in the biodiversity accoutning dataframe.\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0c000d1f",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "# Updates scenario dataframe with scenario info and metrics\n",
    "function update_scenario_record!(\n",
    "    display_scenario_df, cell_cover, sv_reefs, reef_area, iv_num_cells, scen_count\n",
    ")\n",
    "    total_int_area_cm = ((100.0)^2) .* iv_num_cells\n",
    "    rel_cover_sp = dropdims(cell_cover; dims=2) ./ total_int_area_cm\n",
    "\n",
    "    display_scenario_df[scen_count, \"Coral Cover\"] = mean(\n",
    "        rel_cover_sp[:, :, 1]; dims=[1, 2]\n",
    "    )[1]\n",
    "    display_scenario_df[scen_count, \"Coral Cover sd\"] = std(\n",
    "        rel_cover_sp[:, :, 1]; dims=[1, 2]\n",
    "    )[1]\n",
    "\n",
    "    simps_d_temp = simps_D(rel_cover_sp[:, :, 2:7])\n",
    "    display_scenario_df[scen_count, \"Diversity\"] = mean(simps_d_temp; dims=[1, 2])[1]\n",
    "    display_scenario_df[scen_count, \"Diversity sd\"] = std(simps_d_temp; dims=[1, 2])[1]\n",
    "\n",
    "    sv_temp = shelter_vol(sv_reefs, cell_cover, iv_num_cells)\n",
    "    display_scenario_df[scen_count, \"Shelter Volume\"] = mean(sv_temp; dims=[1, 2])[1]\n",
    "    display_scenario_df[scen_count, \"Shelter Volume sd\"] = std(sv_temp; dims=[1, 2])[1]\n",
    "\n",
    "    RBCI_temp = (rel_cover_sp[:, :, 1] .+ simps_d_temp .+ sv_temp) ./ 3\n",
    "    display_scenario_df[scen_count, \"RBCI\"] = mean(RBCI_temp; dims=[1, 2])[1]\n",
    "    display_scenario_df[scen_count, \"RBCI sd\"] = std(RBCI_temp; dims=[1, 2])[1]\n",
    "\n",
    "    display_scenario_df[scen_count, \"Reef k area m2\"] = reef_area\n",
    "\n",
    "    return nothing\n",
    "end\n",
    "\n",
    "# Updates scenario record with intervention - counterfactual data\n",
    "function update_scenario_record!(\n",
    "    display_scenario_df, cell_cover, sv_reefs, iv_num_cells, scen_count\n",
    ")\n",
    "    total_int_area_cm = ((100.0)^2) .* iv_num_cells\n",
    "\n",
    "    rel_cover_sp_iv = dropdims(cell_cover[:, :, :, :, 2]; dims=2) ./ total_int_area_cm\n",
    "    rel_cover_sp_cf = dropdims(cell_cover[:, :, :, :, 1]; dims=2) ./ total_int_area_cm\n",
    "\n",
    "    simps_d_temp_iv = simps_D(rel_cover_sp_iv[:, :, 2:7])\n",
    "    simps_d_temp_cf = simps_D(rel_cover_sp_cf[:, :, 2:7])\n",
    "\n",
    "    sv_temp_iv = shelter_vol(\n",
    "        sv_reefs[:, :, :, :, 2], cell_cover[:, :, :, :, 2], iv_num_cells\n",
    "    )\n",
    "    sv_temp_cf = shelter_vol(\n",
    "        sv_reefs[:, :, :, :, 1], cell_cover[:, :, :, :, 1], iv_num_cells\n",
    "    )\n",
    "\n",
    "    RBCI_temp_iv = (rel_cover_sp_iv .+ simps_d_temp_iv .+ sv_temp_iv) ./ 3\n",
    "    RBCI_temp_cf = (rel_cover_sp_cf .+ simps_d_temp_cf .+ sv_temp_cf) ./ 3\n",
    "\n",
    "    RBCI_uplift = RBCI_temp_iv .- RBCI_temp_cf\n",
    "    display_scenario_df[scen_count, \"RBCI uplift mean\"] = mean(RBCI_uplift; dims=[1, 2])[1]\n",
    "    display_scenario_df[scen_count, \"RBCI uplift sd\"] = std(RBCI_uplift; dims=[1, 2])[1]\n",
    "\n",
    "    return nothing\n",
    "end\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "376416fd",
   "metadata": {},
   "source": [
    "## Load metric calculation functions\n",
    "\n",
    "The following functions calculate the key biodiversity metrics for each reef from cell-level data saved during model runs."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0486f7c0",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Shelter volume metric\n",
    "function shelter_vol(sv_reefs, cell_cover, iv_num_cells)\n",
    "    return sum(\n",
    "        dropdims(sv_reefs; dims=2) .*\n",
    "        dropdims(cell_cover[:, :, :, 2:end] .* 0.0001; dims=2);\n",
    "        dims=3\n",
    "    ) ./ (33 .* (iv_num_cells .* (pi * (0.5)^2)))\n",
    "end\n",
    "\n",
    "# Simpson's diversity metric\n",
    "function simps_D(species_rel_cover)\n",
    "    sum_cover = sum(species_rel_cover; dims=3)\n",
    "    simps_d = 1 .- dropdims(sum((species_rel_cover ./ sum_cover) .^ 2; dims=3); dims=3)\n",
    "    simps_d[isnan.(simps_d)] .= 0.0\n",
    "    return simps_d\n",
    "end\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "06164777",
   "metadata": {},
   "source": [
    "## Load function for creating data summary table \n",
    "\n",
    "The following function is the master function which calls the metrics and data summary functions to build the biodiversity metrics summary data frame. As input, it takes filepaths to the cell-level ReedModEngine.jl dataset and the scenario log dataframe, which specifies which scenarios are counterfactuals and interventions. It also takes in `domain_spatial_df`, a Dataframe containing reef ids, reef area and k proportion for the domain that was run. Note, this spatiual dataframe may not come with the ReefMod domain used for model runs (depending on the version and chosen domain), and so may need to be created separately from available data. Other inputs include a vector of the intervention reef's IDs, and the different assisted adaptation DHW levels and coral deplotment volumes to include."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ee4ead40",
   "metadata": {},
   "outputs": [],
   "source": [
    "function create_metric_summary_dataframe_reefmod(cell_log_filepath,\n",
    "    scenario_par_log_filepath, domain_spatial_df,\n",
    "    iv_reef, aadpt_dhw_range, n_corals_range; rme_spatial_filepath=\"reefmod_gbr.gpkg\",\n",
    "    display_years=[2025, 2030, 2035, 2040, 2045, 2050, 2055, 2059],\n",
    "    save_filename=\"RME_summary_file.csv\")\n",
    "\n",
    "    # Load resultset file (logged cell-level data)\n",
    "    ds = NCDataset(cell_log_filepath)\n",
    "\n",
    "    # Load scenario dataframe and yearly intervention parameters dataframe\n",
    "    scenario_df = CSV.read(scenario_par_log_filepath, DataFrame)\n",
    "    scenario_yearly_iv_df = CSV.read(\n",
    "        string(result_folder, \"\\\\iv_yearly_scenarios.csv\"), DataFrame\n",
    "    )\n",
    "\n",
    "    # Load RME spatial data\n",
    "    rme_spatial = GeoDataFrames.read(rme_spatial_filepath)\n",
    "    longs = rme_spatial.LON\n",
    "    lats = rme_spatial.LAT\n",
    "\n",
    "    # Get intervention years\n",
    "    iv_years = unique(scenario_yearly_iv_df.year)\n",
    "    n_iv_years = length(iv_years) - 1\n",
    "\n",
    "    reef_id_order = findall(in.(rme_spatial.GBRMPA_ID, Ref(domain_spatial_df.reef_id)))\n",
    "    reef_names = rme_spatial.GBRMPA_ID[reef_id_order]\n",
    "    n_reefs = length(reef_names)\n",
    "\n",
    "    reef_areas_vec = domain_spatial_df[:, \"area_km2\"] .* domain_spatial_df[:, \"k\"] .* (10^6)\n",
    "\n",
    "    # Load cell level data on cover, shelter volume, + number of iv cells\n",
    "    cell_cover = ds[\"reef_cell_cover_cm\"][:, :, :, :, :, :]\n",
    "    sv_reefs = ds[\"species_sv_var\"][:, :, :, :, :, :]\n",
    "    iv_num_cells = ds[\"cell_indices_counts\"][:, :]\n",
    "\n",
    "    # Total span of years to include in the dataframe\n",
    "    years = collect(2025:2099)[1:size(reef_cell_cover_cm, 2)]\n",
    "\n",
    "    # Setup storage dataframe\n",
    "    n_scens = (\n",
    "        (\n",
    "            length(display_years) * length(n_corals_range) * length(aadpt_dhw_range) *\n",
    "            n_reefs\n",
    "        ) + length(display_years) * length(n_corals_range) * n_reefs\n",
    "    )\n",
    "    display_scenario_df = DataFrame(\n",
    "        hcat(fill(\"\", n_scens), zeros(n_scens, 18)),\n",
    "        [\"Reef\", \"Year\", \"Intervention\",\n",
    "            \"Deployment Volume\", \"Coral Cover\", \"Coral Cover sd\", \"Diversity\",\n",
    "            \"Diversity sd\", \"Shelter Volume\",\n",
    "            \"Shelter Volume sd\", \"RBCI\", \"RBCI sd\", \"RBCI uplift mean\", \"RBCI uplift sd\",\n",
    "            \"Estimated intervention area m2\",\n",
    "            \"Reef k area m2\", \"RBCI uplift area x estimated intervention area m2\", \"Lat\",\n",
    "            \"Long\"]\n",
    "    )\n",
    "\n",
    "    scen_count = 0\n",
    "\n",
    "    for yr in display_years\n",
    "        # Scenario year index\n",
    "        yr_id = findall(years .== yr)\n",
    "        for rfs in 1:n_reefs\n",
    "            # Scenario reef index\n",
    "            rf_idx = rme_spatial.GBRMPA_ID .== reef_names[rfs]\n",
    "            for nc in n_corals_range\n",
    "\n",
    "                # First, counterfactual scenario\n",
    "                scen_count += 1\n",
    "                display_scenario_df[scen_count, \"Year\"] = yr\n",
    "                display_scenario_df[scen_count, \"Intervention\"] = 0\n",
    "                display_scenario_df[scen_count, \"Deployment Volume\"] = 0\n",
    "                display_scenario_df[scen_count, \"Reef\"] = reef_names[rfs]\n",
    "                display_scenario_df[scen_count, \"Long\"] = longs[rf_idx]\n",
    "                display_scenario_df[scen_count, \"Lat\"] = lats[rf_idx]\n",
    "\n",
    "                # Coral scenario idx (needed as RME has paired counterfactual and intervention runs)\n",
    "                coral_scens = (scenario_df.n_corals .== nc)\n",
    "\n",
    "                # Add metrics to dataframe for CF\n",
    "                update_scenario_record!(display_scenario_df,\n",
    "                    cell_cover[findall(coral_scens), yr_id, :, rfs, :, 1],\n",
    "                    sv_reefs[findall(coral_scens), yr_id, :, rfs, :, 1],\n",
    "                    reef_areas_vec[rfs],\n",
    "                    iv_num_cells[findall(coral_scens), :], scen_count)\n",
    "\n",
    "                for aa in aadpt_dhw_range\n",
    "                    # Assisted adaptation level ID\n",
    "                    scen_ids = findall(coral_scens .& (scenario_df.a_adapt .== aa))\n",
    "\n",
    "                    # Update data for intervention scenario\n",
    "                    scen_count += 1\n",
    "                    display_scenario_df[scen_count, \"Year\"] = yr\n",
    "                    display_scenario_df[scen_count, \"Intervention\"] = aa\n",
    "                    display_scenario_df[scen_count, \"Deployment Volume\"] = nc\n",
    "                    display_scenario_df[scen_count, \"Reef\"] = reef_names[rfs]\n",
    "                    display_scenario_df[scen_count, \"Long\"] = longs[rf_idx]\n",
    "                    display_scenario_df[scen_count, \"Lat\"] = lats[rf_idx]\n",
    "\n",
    "                    # If the reef is an intervention reef, calculate actual number of corals outplanted\n",
    "                    if reef_names[rfs] == iv_reef\n",
    "                        iv_id = findall(\n",
    "                            (scenario_df.a_adapt .== aa) .& (scenario_df.n_corals .== nc)\n",
    "                        )\n",
    "                        if yr <= iv_years[end]\n",
    "                            # If year is within the intervention time frame, add all corals outplanted up to this year\n",
    "                            actual_n_corals =\n",
    "                                ((\n",
    "                                    mean(\n",
    "                                        scenario_yearly_iv_df[\n",
    "                                            in.(\n",
    "                                                scenario_yearly_iv_df[:, \"intervention id\"],\n",
    "                                                Ref(iv_id)\n",
    "                                            ),\n",
    "                                            \"number of corals\"\n",
    "                                        ]\n",
    "                                    ) / n_iv_years\n",
    "                                )) * findfirst(iv_years .== yr)\n",
    "                        else\n",
    "                            actual_n_corals = ((mean(\n",
    "                                scenario_yearly_iv_df[\n",
    "                                    in.(\n",
    "                                        scenario_yearly_iv_df[:, \"intervention id\"],\n",
    "                                        Ref(iv_id)\n",
    "                                    ),\n",
    "                                    \"number of corals\"\n",
    "                                ]\n",
    "                            )))\n",
    "                        end\n",
    "                        # Estimate intervention area as number of corals/5 corals/m2\n",
    "                        display_scenario_df[scen_count, \"Estimated intervention area m2\"] =\n",
    "                            actual_n_corals / 5\n",
    "                    end\n",
    "\n",
    "                    # Update metrics for intervention scenario\n",
    "                    update_scenario_record!(\n",
    "                        display_scenario_df,\n",
    "                        cell_cover[scen_ids, yr_id, :, rfs, :, 2],\n",
    "                        sv_reefs[scen_ids, yr_id, :, rfs, :, 2],\n",
    "                        reef_areas_vec[rfs],\n",
    "                        iv_num_cells[scen_ids, :],\n",
    "                        scen_count\n",
    "                    )\n",
    "                    # Add intervention minus counterfactual metrics\n",
    "                    update_scenario_record!(\n",
    "                        display_scenario_df,\n",
    "                        cell_cover[scen_ids, yr_id, :, rfs, :, :],\n",
    "                        sv_reefs[scen_ids, yr_id, :, rfs, :, :],\n",
    "                        iv_num_cells[scen_ids, :],\n",
    "                        scen_count\n",
    "                    )\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # Add column with estimated intervention area * RBCI uplift\n",
    "    display_scenario_df[:, \"RBCI uplift area m2 x estimated intervention area\"] .=\n",
    "        display_scenario_df[:, \"Estimated intervention area m2\"] .*\n",
    "        display_scenario_df[:, \"RBCI uplift mean\"]\n",
    "    # Save as CSV\n",
    "    CSV.write(save_filename, display_scenario_df)\n",
    "\n",
    "    return display_scenario_df\n",
    "end\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "ee1ff016",
   "metadata": {},
   "source": [
    "## Key filenames from ReefModEngine.jl dataset \n",
    "\n",
    "To run the master function, the following filenames and inputs need to be defined. `cell_log_filepath` is the filepath to the netcdf saved during model runs which contains cell-level records of coral cover and shelter volume. `scenario_par_log_filepath` is the csv file recording scenario IDs for the runs, and flagging which scenarios are interventions versus counterfactuals. `result_folder` is the filepath to the automatically saved results folder (reef level data). `domain_spatial_df` is a Dataframe containing reef ids, reef area and k proportion for the domain that was run."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "954a5af6",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Filepath for netcdf which has cell-level data saved\n",
    "cell_log_filepath = \"cell_log_2025marchmilestoneruns.nc\"\n",
    "# Filepath to CSV storing info on intervention scenario parameters\n",
    "scenario_par_log_filepath = \"scenario_par_log_2025milestoneruns.csv\"\n",
    "# Folder containing RME resultset\n",
    "result_folder = \"ext_moore_results_2025milestoneruns\"\n",
    "# Dataframe containing domain of interest, reef ids, reef area and k proportion\n",
    "reef_areas_file = \"ext_moore_areas.csv\"\n",
    "domain_spatial_df = CSV.read(reef_areas_file, DataFrame)\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "fcde0e69",
   "metadata": {},
   "source": [
    "## Key scenarios to include in dataframe\n",
    "\n",
    "Other inputs to the master function include the assisted adaptation DHW levels and coral deployment volumes to include, and the intervention reef ID(s).\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "4a15c799",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Scenario parameters to include\n",
    "aadpt_dhw_range = [5.0, 10.0, 20.0] # assisted adaptation ranges\n",
    "n_corals_range = [1000000] # deployment volume ranges\n",
    "iv_reef = [\"16-071\"] # Intervention reef/reefs ID"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "227ecbda",
   "metadata": {},
   "source": [
    "## Create biodiversity metric summary dataframe \n",
    "\n",
    "Finally, run the master function to create the biodiversity metrics summary dataframe and save it as a csv. The function outputs the dataframe itself. The filepath to save it at can be specified in the input `save_filename`."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "9a682f6e",
   "metadata": {},
   "outputs": [],
   "source": [
    "summ_data_frame = create_metric_summary_dataframe_reefmod(cell_log_filepath,\n",
    "    scenario_par_log_filepath, domain_spatial_df,\n",
    "    iv_reef, aadpt_dhw_range, n_corals_range;\n",
    "    save_filename=\"scenario_log_biodiversity_milestone_march2025_ExtMooreRMEruns.csv\")"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.5",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
